@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Base Process Order Details'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
define view entity ZR_RJ_ProcessOrderDetail
  as select from I_ManufacturingOrder as OrderHeader
    inner join   I_Order              as OrderMaster on  OrderHeader.ManufacturingOrder = OrderMaster.OrderID
                                                     and OrderMaster.OrderCategory      = '40'
  association        to ZR_RJ_ReservationDetails_TF as _ReservationDetails on  $projection.Reservation = _ReservationDetails.rsnum
  association [0..1] to I_ProductText               as _MatText            on  $projection.Material = _MatText.Product
                                                                           and _MatText.Language    = $session.system_language
  association        to ZR_RJ_ProcessOrderStatus_TF as _Status             on  $projection.ObjectInternalID = _Status.objnr

{
  key OrderHeader.ManufacturingOrder,           //AFKO-AUFNR
      OrderHeader.MfgOrderScheduledStartDate,   //AFKO-GSTRS
      OrderHeader.MfgOrderScheduledEndDate,     //AFKO-GLTRS
      OrderHeader.Reservation,                  //AFKO-RSNUM
      @Semantics.quantity.unitOfMeasure: 'ProductionUnit'
      OrderHeader.MfgOrderPlannedTotalQty,      //AFKO-GAMNG
      OrderHeader.ProductionUnit,               //AFKO-GMEIN
      OrderHeader.Material,                     //AFKO-PLNBEZ
      OrderHeader.MfgOrderInternalID,           //AFKO-AUFPL
      OrderHeader.ManufacturingOrderImportance, //AFKO-APRIO
      OrderHeader.MfgOrderScheduledEndTime,     //AFKO-GLUZS
      OrderHeader.MfgOrderScheduledStartTime,   //AFKO-GSUZS
      OrderHeader.OrderSequenceNumber,          //AFKO-CY_SEQNR
      OrderHeader.BillOfMaterialVariant,        //AFKO-STLAL
      OrderHeader.ActualCostsCostingVariant,    //AFKO-KLVARI  
      OrderMaster.ObjectInternalID,             //AUFK-OBJNR
      _ReservationDetails,                      //RESB & MAKT
      _MatText,                                 //MAKT
      _Status                                   //JEST & TJ02T
}
