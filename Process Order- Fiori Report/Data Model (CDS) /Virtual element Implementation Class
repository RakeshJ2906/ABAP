CLASS zcl_rj_po_read_text DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES if_sadl_exit.
    INTERFACES if_sadl_exit_calc_element_read.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.



CLASS zcl_rj_po_read_text IMPLEMENTATION.

  METHOD if_sadl_exit_calc_element_read~calculate.

    DATA: lt_process_order TYPE STANDARD TABLE OF zc_rj_processorderdetail,
          lt_txt_lines     TYPE STANDARD TABLE OF tline.

    DATA: lv_txt_name TYPE thead-tdname.

    CHECK it_original_data IS NOT INITIAL.
    lt_process_order = CORRESPONDING #( it_original_data ).

    LOOP AT lt_process_order ASSIGNING FIELD-SYMBOL(<fs_process_order>).
      CONCATENATE sy-mandt <fs_process_order>-manufacturingorder INTO lv_txt_name.
      CALL FUNCTION 'READ_TEXT'
        EXPORTING
          client                  = sy-mandt
          id                      = 'KOPF'
          language                = sy-langu
          name                    = lv_txt_name
          object                  = 'AUFK'
        TABLES
          lines                   = lt_txt_lines
        EXCEPTIONS
          id                      = 1
          language                = 2
          name                    = 3
          not_found               = 4
          object                  = 5
          reference_check         = 6
          wrong_access_to_archive = 7
          OTHERS                  = 8.
      IF sy-subrc IS INITIAL.
        LOOP AT lt_txt_lines INTO DATA(ls_lines).
          CONCATENATE <fs_process_order>-ltext ls_lines-tdline INTO <fs_process_order>-ltext.
        ENDLOOP.
      ENDIF.
    ENDLOOP.

    ct_calculated_data = CORRESPONDING #( lt_process_order ).

  ENDMETHOD.

  METHOD if_sadl_exit_calc_element_read~get_calculation_info.

  ENDMETHOD.

ENDCLASS.
