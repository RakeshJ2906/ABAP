@AccessControl.authorizationCheck: #NOT_REQUIRED
@AbapCatalog.extensibility.extensible: true
@EndUserText.label: 'Consumption CDS View for Booking'
@Metadata.ignorePropagatedAnnotations: false
@Metadata.allowExtensions: true
define view entity ZRAP_CV_BOOKING
  as projection on ZRAP_IV_BOOKING as Booking
{
  key TravelId,
  key BookingId,
      BookingDate,
      @Consumption.valueHelpDefinition: [{ entity : {name: '/DMO/I_Customer', element: 'CustomerID'} }]
      @ObjectModel.text.element: ['CustomerName']
      CustomerId,
      _Customer.LastName as CustomerName,
      @Consumption.valueHelpDefinition: [{entity: {name: '/DMO/I_Carrier', element: 'AirlineID' }}]
      @ObjectModel.text.element: ['CarrierName']
      CarrierId,
      _Carrier.Name as CarrierName,
      @Consumption.valueHelpDefinition: [ {entity: {name: '/DMO/I_Flight', element: 'ConnectionID'},
                                             additionalBinding: [ { localElement: 'CarrierID',    element: 'AirlineID' },
                                                                  { localElement: 'FlightDate',   element: 'FlightDate',   usage: #RESULT},
                                                                  { localElement: 'FlightPrice',  element: 'Price',        usage: #RESULT },
                                                                  { localElement: 'CurrencyCode', element: 'CurrencyCode', usage: #RESULT } ] } ]
      ConnectionId,
      FlightDate,
      FlightPrice,
      @Consumption.valueHelpDefinition: [{entity: {name: 'I_Currency', element: 'Currency' }}]
      CurrencyCode,
      @Consumption.valueHelpDefinition: [{entity: {name: '/DMO/I_Booking_Status_VH', element: 'BookingStatus' }}]
      @ObjectModel.text.element: ['BookingStatusText']
      BookingStatus,
      _Booking_Status._Text.Text as BookingStatusText : localized,
      CreatedBy,
      LastChangedBy,
      LocalLastChangedAt,

      /* Associations */
      _Travel    : redirected to parent ZRAP_CV_TRAVEL,
      _BookSuppl : redirected to composition child ZRAP_CV_BOOK_SUP,
      _Customer,
      _Carrier,
      _Connection,
      _Flight,
      _Currency,
      _Booking_Status,
      _AirlineURL
}
