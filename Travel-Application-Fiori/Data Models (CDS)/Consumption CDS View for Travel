@AccessControl.authorizationCheck: #NOT_REQUIRED
@AbapCatalog.extensibility.extensible: true
@EndUserText.label: 'Consumption CDS View for Travel'
@Metadata.ignorePropagatedAnnotations: false
@Metadata.allowExtensions: true
@ObjectModel.semanticKey: ['TravelId']
define root view entity ZRAP_CV_TRAVEL
  provider contract transactional_query
  as projection on ZRAP_IV_TRAVEL as Travel
{
      @Search.defaultSearchElement: true
      @ObjectModel.text.element: ['Description']
  key TravelId,
      @Consumption.valueHelpDefinition: [{ entity: { name: '/DMO/I_Agency', element: 'AgencyID'} }]
      @ObjectModel.text.element: ['AgencyName']
      AgencyId,
      _Agency.Name as AgencyName,
      @Consumption.valueHelpDefinition: [{ entity: { name: '/DMO/I_Customer', element: 'CustomerID'} }]
      @ObjectModel.text.element: ['CustomerName']
      CustomerId,
      _Customer.LastName as CustomerName,
      BeginDate,
      EndDate,
      BookingFee,
      TotalPrice,
      @Consumption.valueHelpDefinition: [{ entity: { name: 'I_Currency', element: 'Currency'} }]
      CurrencyCode,
      Description,
      @Consumption.valueHelpDefinition: [{ entity: { name: 'ZRAP_IV_STATUS', element: 'OverallStatus'} }]
      @ObjectModel.text.element: [ 'Status' ]
      TravelStatus,
      _Status.Text as Status : localized,
      StatusCriticality,
      CreatedBy,
      CreatedAt,
      LastChangedBy,
      LastChangedAt,
      LocalLastChangedAt,

      /* Associations */
      _Booking : redirected to composition child ZRAP_CV_BOOKING,
      _Agency,
      _Customer,
      _Currency,
      _Status
}
