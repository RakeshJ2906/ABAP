//@AbapCatalog.viewEnhancementCategory: [#NONE]
@AbapCatalog.extensibility.extensible: true
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Interface CDS View for Booking'
@Metadata.ignorePropagatedAnnotations: false
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
define view entity ZRAP_IV_BOOKING
  as select from zrap_tb_booking as Booking

  association        to parent ZRAP_IV_TRAVEL    as _Travel         on  $projection.TravelId = _Travel.TravelId
  composition [0..*] of ZRAP_IV_BOOK_SUP         as _BookSuppl
  association [1..1] to /DMO/I_Customer          as _Customer       on  $projection.CustomerId = _Customer.CustomerID
  association [1..1] to /DMO/I_Carrier           as _Carrier        on  $projection.CarrierId = _Carrier.AirlineID
  association [1..1] to /DMO/I_Connection        as _Connection     on  $projection.CarrierId    = _Connection.AirlineID
                                                                    and $projection.ConnectionId = _Connection.ConnectionID
  association [1..1] to /DMO/I_Flight            as _Flight         on  $projection.CarrierId    = _Flight.AirlineID
                                                                    and $projection.ConnectionId = _Flight.ConnectionID
                                                                    and $projection.FlightDate   = _Flight.FlightDate
  association [0..1] to I_Currency               as _Currency       on  $projection.CurrencyCode = _Currency.Currency
  association [1..1] to /DMO/I_Booking_Status_VH as _Booking_Status on  $projection.BookingStatus = _Booking_Status.BookingStatus
  association [1..1] to ZRAP_IV_AIRLINE_URL      as _AirlineURL     on  $projection.CarrierId = _AirlineURL.CarrierId
{
  key travel_id             as TravelId,
  key booking_id            as BookingId,
      booking_date          as BookingDate,
      customer_id           as CustomerId,
      carrier_id            as CarrierId,
      connection_id         as ConnectionId,
      flight_date           as FlightDate,
      @Semantics.amount.currencyCode: 'CurrencyCode'
      flight_price          as FlightPrice,
      currency_code         as CurrencyCode,
      booking_status        as BookingStatus,
      @Semantics.user.createdBy: true
      created_by            as CreatedBy,
      @Semantics.user.lastChangedBy: true
      last_changed_by       as LastChangedBy,
      @Semantics.systemDateTime.localInstanceLastChangedAt: true
      local_last_changed_at as LocalLastChangedAt,

      /* associations */
      _Travel,
      _BookSuppl,
      _Customer,
      _Carrier,
      _Connection,
      _Flight,
      _Currency,
      _Booking_Status,
      _AirlineURL
}

