managed implementation in class zbp_rap_iv_travel unique;
strict ( 2 );
extensible;
with draft;

define behavior for ZRAP_IV_TRAVEL alias Travel
persistent table zrap_tb_travel
draft table zrap_dtb_travel
lock master total etag LastChangedAt
authorization master ( instance )
etag master LocalLastChangedAt
late numbering
extensible
{
  //create ( authorization : global );
  create { default function GetDefaultsForCreate; }
  update;
  delete;
  association _Booking { create; with draft; }

  field ( readonly ) TravelId;
  field ( readonly ) LastChangedAt, LastChangedBy, CreatedAt, CreatedBy, LocalLastChangedAt;
  field ( mandatory ) AgencyID, CustomerID;

  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;
  action ( features : instance ) bookingfeeDiscount parameter /dmo/a_travel_discount result [1] $self;
  internal action recalcTotalPrice;

  //    determination setInitialStatus on modify { create; }
  determination calculateTotalPrice on modify { field BookingFee, CurrencyCode; }

  side effects
  { field BookingFee affects field TotalPrice;
    field CurrencyCode affects field TotalPrice; }

  validation validateAgency on save { field AgencyId; create; update; }
  validation validateCustomer on save { field CustomerId; create; update; }
  validation validateDates on save { field BeginDate, EndDate; create; update; }

  draft action Activate optimized;
  draft action Discard;
  draft action Edit;
  draft action Resume;
  draft determine action Prepare
  {
    validation validateAgency;
    validation validateCustomer;
    validation validateDates;
  }

  mapping for zrap_tb_travel corresponding extensible
    {
      TravelId           = travel_id;
      AgencyId           = agency_id;
      CustomerId         = customer_id;
      BeginDate          = begin_date;
      EndDate            = end_date;
      BookingFee         = booking_fee;
      TotalPrice         = total_price;
      CurrencyCode       = currency_code;
      Description        = description;
      TravelStatus       = overall_status;
      CreatedBy          = created_by;
      CreatedAt          = created_at;
      LastChangedBy      = last_changed_by;
      LastChangedAt      = last_changed_at;
      LocalLastChangedAt = local_last_changed_at;
    }

}

define behavior for ZRAP_IV_BOOKING alias Booking
persistent table zrap_tb_booking
draft table zrap_dtb_booking
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
late numbering
extensible
{
  update;
  delete;
  association _Travel { with draft; }
  association _BookSuppl { create{ default function GetDefaultsForBookSuppl; } with draft; }

  field ( readonly ) TravelId, BookingId;
  field ( readonly ) CreatedBy, LastChangedBy, LocalLastChangedAt;

  determination calculateTotalPrice on modify { field FlightPrice, CurrencyCode; }
  side effects
  { field FlightPrice affects field _Travel.TotalPrice;
    field CurrencyCode affects field _Travel.TotalPrice; }

  mapping for zrap_tb_booking corresponding extensible
    {
      TravelId           = travel_id;
      BookingId          = booking_id;
      BookingDate        = booking_date;
      CustomerID         = customer_id;
      CarrierID          = carrier_id;
      ConnectionID       = connection_id;
      FlightDate         = flight_date;
      FlightPrice        = flight_price;
      CurrencyCode       = currency_code;
      BookingStatus      = booking_status;
      CreatedBy          = created_by;
      LastChangedBy      = last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
    }

}

define behavior for ZRAP_IV_BOOK_SUP alias BookSuppl
persistent table zrap_tb_book_sup
draft table zrap_dtb_book_sp
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
late numbering
extensible
{
  update;
  delete;
  association _Travel { with draft; }
  association _Booking { with draft; }

  field ( readonly ) TravelId, BookingId, BookingSupplementId;
  field ( readonly ) CreatedBy, LastChangedBy, LocalLastChangedAt;

  mapping for zrap_tb_book_sup corresponding extensible
    {
      TravelId            = travel_id;
      BookingId           = booking_id;
      BookingSupplementId = booking_supplement_id;
      SupplementId        = supplement_id;
      Price               = price;
      CurrencyCode        = currency_code;
      CreatedBy           = created_by;
      LastChangedBy       = last_changed_by;
      LocalLastChangedAt  = local_last_changed_at;
    }
}
