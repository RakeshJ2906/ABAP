@Metadata.layer: #CORE
@Search.searchable: true
@UI: { headerInfo: { typeName: 'Travel',
                     typeNamePlural: 'Travels',
                     title: { type: #STANDARD, label: 'Travel', value: 'TravelId' },
                     description: { value: 'Description' }
                   },
       presentationVariant: [{ sortOrder: [{ by: 'TravelId', direction:  #DESC }],
                               visualizations: [{ type: #AS_LINEITEM }] }] }

@UI.lineItem: [{ criticality: 'StatusCriticality' }]
annotate view ZRAP_CV_TRAVEL with
{
  @UI.facet: [
              { label: 'Bookings',
                id: 'Booking',
                purpose: #STANDARD,
                type: #LINEITEM_REFERENCE,
                position: 10,
                targetElement: '_Booking'
              },

              { id: 'TravelHeaderPrice',
                purpose: #HEADER,
                type: #DATAPOINT_REFERENCE,
                position: 10,
                targetQualifier: 'PriceData'
              },

              { id: 'TravelHeaderOverallStatus',
                purpose: #HEADER,
                type: #DATAPOINT_REFERENCE,
                position: 20,
                targetQualifier: 'StatusData'
              },

              { label: 'General Information',
                id: 'GeneralInfo',
                type: #COLLECTION,
                position: 10
              },

              { label: 'Basic Data',
                id: 'Travel',
                type: #IDENTIFICATION_REFERENCE,
                purpose: #STANDARD,
                parentId: 'GeneralInfo',
                position: 10
              },

              { label: 'Prices',
                id: 'Prices',
                purpose: #STANDARD,
                type: #FIELDGROUP_REFERENCE,
                parentId: 'GeneralInfo',
                position: 20,
                targetQualifier: 'PricesGroup'
              },

              { label: 'Dates',
                id: 'Dates',
                purpose: #STANDARD,
                type: #FIELDGROUP_REFERENCE,
                parentId: 'GeneralInfo',
                position: 30,
                targetQualifier: 'DatesGroup'
              },

              { label: 'Administrative Data',
                id: 'CreationDetails',
                purpose: #STANDARD,
                type: #FIELDGROUP_REFERENCE,
                parentId: 'GeneralInfo',
                position: 40,
                targetQualifier: 'AdmDataGroup'
              }

             ]

  @UI: { lineItem: [ { position: 10 } ] }
  TravelId;
  @UI: { lineItem:       [ { position: 20 } ],
         selectionField: [ { position: 10 } ],
         identification: [ { position: 10 } ] }
  @Search.defaultSearchElement: true
  AgencyId;
  @UI: { lineItem:       [ { position: 30 } ],
         selectionField: [ { position: 20 } ],
         identification: [ { position: 20 } ] }
  @Search.defaultSearchElement: true
  CustomerId;
  @UI: { lineItem:   [ { position: 40 } ],
         fieldGroup: [ { qualifier: 'DatesGroup', position: 10 }] }
  BeginDate;
  @UI: { lineItem:   [ { position: 50 } ],
         fieldGroup: [ { qualifier: 'DatesGroup', position: 20 }] }
  EndDate;
  @UI: { fieldGroup: [ { qualifier: 'PricesGroup', position: 10 }] }
  BookingFee;
  @UI: {  lineItem:   [ { position: 60 } ],
          fieldGroup: [ { qualifier: 'PricesGroup', position: 20 }],
          dataPoint:  { qualifier: 'PriceData', title: 'Total Price' }
       }
  TotalPrice;
  @Search.defaultSearchElement: true
  @Search.fuzzinessThreshold: 0.7
  @UI: { identification: [{ position: 30 }] }
  Description;
  @UI: { lineItem: [ { position: 70, criticality: 'StatusCriticality', criticalityRepresentation: #WITH_ICON },
                     { type: #FOR_ACTION, dataAction: 'acceptTravel', label: 'Accept Travel', position: 10 },
                     { type: #FOR_ACTION, dataAction: 'rejectTravel', label: 'Reject Travel', position: 20 },
                     { type: #FOR_ACTION, dataAction: 'copyRecord', label: 'Copy Travel', position: 30 }
                   ],
         identification: [
                           { type: #FOR_ACTION_GROUP, label: 'Change Status', actionGroupId: 'menu-ChangeStatus' },
                           { type: #FOR_ACTION, dataAction: 'acceptTravel', label: 'Accept Travel', position: 20, actionGroupId: 'menu-ChangeStatus' },
                           { type: #FOR_ACTION, dataAction: 'rejectTravel', label: 'Reject Travel', position: 30, actionGroupId: 'menu-ChangeStatus' },
                           { type: #FOR_ACTION, dataAction: 'bookingfeeDiscount', label: 'Apply discount', position: 10 }
                         ],
         selectionField: [ { position: 30 } ],
         dataPoint: { qualifier: 'StatusData', title: 'Status', criticality: 'StatusCriticality', criticalityRepresentation: #WITH_ICON }
       }
  @UI.textArrangement: #TEXT_ONLY
  TravelStatus;
  @UI: { fieldGroup: [ { qualifier: 'AdmDataGroup', position: 10, label: 'Create By' }] }
  CreatedBy;
  @UI: { fieldGroup: [ { qualifier: 'AdmDataGroup', position: 20, label: 'Created At' }] }
  CreatedAt;
  @UI.hidden: true
  LastChangedBy;
  @UI.hidden: true
  LastChangedAt;
  @UI.hidden: true
  LocalLastChangedAt;

}
