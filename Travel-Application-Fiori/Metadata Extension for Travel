@Metadata.layer: #CORE
@UI: {
  headerInfo: { typeName: 'Travel',
                typeNamePlural: 'Travels',
                title: { type: #STANDARD, label: 'Travel', value: 'TravelID' },
                description: { value: 'Description' } 
              },
  presentationVariant: [{ sortOrder: [{ by: 'TravelID', direction:  #DESC }],
                          visualizations: [{ type: #AS_LINEITEM }] }] }

annotate view ZRAP_CV_TRAVEL with
{
  @UI.facet: [ 
  
              { label: 'Bookings',
                id: 'Booking',
                purpose: #STANDARD,
                type: #LINEITEM_REFERENCE,
                position: 20,
                targetElement: '_Booking'
              },
               
              { id: 'TravelHeaderPrice',
                purpose: #HEADER,
                type: #DATAPOINT_REFERENCE,
                position: 10,
                targetQualifier: 'PriceData' 
              },
               
              { id: 'TravelHeaderOverallStatus',
                purpose: #HEADER,
                type: #DATAPOINT_REFERENCE,
                position: 20,
                targetQualifier: 'StatusData'
              },
               
              { label: 'General Information',
                id: 'GeneralInfo',
                type: #COLLECTION,
                position: 10 
              },
             
              { label: 'General',
                id: 'Travel',
                type: #IDENTIFICATION_REFERENCE,
                purpose: #STANDARD,
                parentId: 'GeneralInfo',
                position: 10 
              },
              
              { label: 'Prices',
                id: 'Prices',
                purpose: #STANDARD,
                type: #FIELDGROUP_REFERENCE,
                parentId: 'GeneralInfo',
                position: 20,
                targetQualifier: 'PricesGroup' 
              },
              
              { label: 'Dates',
                id: 'Dates',
                purpose: #STANDARD,
                type: #FIELDGROUP_REFERENCE,
                parentId: 'GeneralInfo',
                position: 30,
                targetQualifier: 'DatesGroup' 
              }
                   
             ]

  //@UI:{ identification: [{ position: 1, label: 'Travel UUID' }] }
  @UI.hidden: true
  TravelUUID;

  @UI: { lineItem: [ { position: 10, importance: #HIGH } ] }
  TravelID;
  
  @UI: { identification: [ { position: 10 } ] }
  Description;

  @UI: {  lineItem:       [ { position: 20, importance: #HIGH } ],
          selectionField: [ { position: 10 } ], 
          identification: [ { position: 20 } ] }
  AgencyID;

  @UI: {  lineItem:       [ { position: 30, importance: #HIGH } ],
          selectionField: [ { position: 20 } ],
          identification: [ { position: 30 } ] }
  CustomerID;

  @UI: {  lineItem:   [ { position: 40, importance: #HIGH } ],
          fieldGroup: [ { qualifier: 'DatesGroup', position: 10 }] }
  BeginDate;

  @UI: {  lineItem:   [ { position: 50,importance: #HIGH } ],
          fieldGroup: [ { qualifier: 'DatesGroup', position: 20 }] }
  EndDate;

  @UI: {  lineItem:   [ { position: 60, importance: #HIGH } ],
          fieldGroup: [ { qualifier: 'PricesGroup', position: 10 }] }
  BookingFee;

  @UI: {  lineItem:   [ { position: 70, importance: #HIGH } ],
          fieldGroup: [ { qualifier: 'PricesGroup', position: 20 }],
          dataPoint: { qualifier: 'PriceData', title: 'Total Price' } 
       }
  TotalPrice;

  @UI: { lineItem:       [ { position: 90, importance: #HIGH, criticality: 'StatusCriticality' },
                           { type: #FOR_ACTION, dataAction: 'acceptTravel', label: 'Accept Travel' },
                           { type: #FOR_ACTION, dataAction: 'rejectTravel', label: 'Reject Travel' }
                         ],
         identification: [ 
                           { type: #FOR_ACTION, dataAction: 'acceptTravel', label: 'Accept Travel' },
                           { type: #FOR_ACTION, dataAction: 'rejectTravel', label: 'Reject Travel' },
                           { type: #FOR_ACTION, dataAction: 'bookingfeeDiscount', label: 'Apply discount' }
                         ],
         selectionField: [ { position: 30 } ],
         dataPoint: { qualifier: 'StatusData', title: 'Status', criticality: 'StatusCriticality' }  
       } 
  @UI.textArrangement: #TEXT_ONLY
  TravelStatus;

  @UI.hidden: true
  LastChangedAt;

  @UI.hidden: true
  LocalLastChangedAt;

}
